import com.adyen.sdk.Service
import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'adyen.sdk-automation-conventions'
}

project.ext {
    generator = 'csharp'
    templates = 'templates-v7/csharp/libraries/generichost'
}

def services = project.ext.services as List<Service>

services.each { Service svc ->
    def serviceName = project.ext.serviceNaming[svc.id] as String

    // Generation
    tasks.named("generate${svc.name}", GenerateTask) {
        println "Generating ${svc.name}..."
        apiPackage.set("${svc.name}.Services")
        apiNameSuffix.set("Service")
        modelPackage.set("${svc.name}.Models")
/*        reservedWordsMappings.putAll([
                "version": "Version",
//                "Version": "Version",
//                "VarVersion": "Version",
//                "varVersion": "Version",
//                "timeZone": "TimeZone",
//                "environment": "Environment"
        ])*/
//        parameterNameMappoing.putAll([
//                "gag": "Dsgs"
//        ])

        additionalProperties.putAll([
                // Target framework version.
                'targetFramework': 'net8.0',

                // Use library: generichost.
                'library': 'generichost',

                // Your C# package name (convention: CamelCase).
                'packageName': 'Adyen',

                // The name of your core-folder.
                'corePackageName': 'Core',

                // Service -suffix
                'apiName': svc.name,

                // Make API response's headers case-insensitive
                'caseInsensitiveResponseHeaders': 'false',

                // Override Equals and GetHashCode methods
                'equatable': 'true',

                // Name of the API service.
                'serviceName': serviceName,

                // Use nullable annotations in the project, only supported on C# 8 & ASP.NET Core 3.1+
                // Starting in .NET 6.0 the default is true.
                'nullableReferenceTypes': 'true',

                // Use DateTime to model date properties even if DateOnly supported. (.NET 6.0+ only)
                'useDateTimeOffset': 'true',

                // Use the discriminator's mapping in oneOf to speed up the model lookup.
                // IMPORTANT NOTE: Validation (e.g. one and only one match in oneOf's schemas) will be skipped.
                'useOneOfDiscriminatorLookup': 'true',

                // Source generation is a feature that lets the compiler generate optimized code at build time, making JSON (de)serialization faster.
                // This flag uses the SourceGenerationContext.mustache template to:
                // 1. `WriteIndented = true` -> Prettify, by indenting the JSON
                // 2. `GenerationMode = JsonSourceGenerationMode.Metadata | JsonSourceGenerationMode.Serialization`
                //     This sets the generation mode using a bitwise OR between two flags:
                //     - Metadata: Generates metadata needed to support reflection-free serialization.
                //     - Serialization: Generates methods for serializing objects to JSON.
                //'useSourceGeneration': 'false',
        ])
    }

    // Deployment
    def deployModels = tasks.register("generate${svc.name}Models", Sync) {
        println "Moving ${svc.name}-Models..."
        group "deploy"
        description "Move ${svc.name} models into the repo."
        dependsOn "generate${svc.name}"
        outputs.upToDateWhen { false }

        // Delete existing models.
        doFirst {
            delete layout.projectDirectory.dir("repo/Adyen/" + serviceName + "/Models")
        }

        from layout.buildDirectory.dir("services/$svc.id/src/Adyen/${serviceName}.Models")
        into layout.projectDirectory.dir("repo/Adyen/" + serviceName + "/Models")
    }

    def deployServices = tasks.register("generate${svc.name}Services", Sync) {
        println "Moving ${svc.name}-Services..."
        group "deploy"
        description "Move ${svc.name} into the repo."
        dependsOn "generate${svc.name}Models"
        outputs.upToDateWhen { false }

        // Delete existing services.
        doFirst {
            delete layout.projectDirectory.dir("repo/Adyen/" + serviceName + "/Services")
        }

        from(layout.buildDirectory.dir("services/${svc.id}/src/Adyen/${serviceName}.Services").get().asFile) {
            exclude 'IApi.cs'
        }
        into layout.projectDirectory.dir("repo/Adyen/" + serviceName + "/Services")
    }

    def deployClients = tasks.register("generate${svc.name}Clients", Sync) {
        println "Moving ${svc.name}-Clients..."
        group "deploy"
        description "Move ${svc.name} into the repo."
        dependsOn "generate${svc.name}Services"
        outputs.upToDateWhen { false }

        // Delete existing clients.
        doFirst {
            delete layout.projectDirectory.dir("repo/Adyen/" + serviceName + "/Clients")
        }

        from (layout.buildDirectory.dir("services/$svc.id/src/Adyen/Client").get().asFile) {
            include 'HostConfiguration.cs', 'ClientUtils.cs', 'JsonSerializerOptionsProvider.cs'
        }
        into layout.projectDirectory.dir("repo/Adyen/" + serviceName + "/Client")
    }

    def deployExtensions = tasks.register("generate${svc.name}Extensions", Sync) {
        println "Moving ${svc.name}-Extensions..."
        group "generate"
        description "Move ${svc.name} into the repo."
        dependsOn "generate${svc.name}Clients"
        outputs.upToDateWhen { false }

        // Delete existing extensions.
        doFirst {
            delete layout.projectDirectory.dir("repo/Adyen/" + serviceName + "/Extensions")
        }

        from (layout.buildDirectory.dir("services/$svc.id/src/Adyen/Extensions").get().asFile) {
            include 'IHostBuilderExtensions.cs', 'IServiceCollectionExtensions.cs'
            rename('IHostBuilderExtensions.cs', 'HostBuilderExtensions.cs')
            rename('IServiceCollectionExtensions.cs', 'ServiceCollectionExtensions.cs')
        }
        into layout.projectDirectory.dir("repo/Adyen/" + serviceName + "/Extensions")
    }

    def deployDocumentation = tasks.register("generate${svc.name}Documentation", Sync) {
        println "Moving ${svc.name}-Documentation..."
        group "generate"
        description "Move ${svc.name} into the repo."
        dependsOn "generate${svc.name}Services"
        outputs.upToDateWhen { false }

        // Delete existing docs.
        doFirst {
            delete layout.projectDirectory.dir("repo/docs/" + serviceName)
        }

        from (layout.buildDirectory.dir("services/$svc.id/docs/apis").get().asFile)
        into layout.projectDirectory.dir("repo/docs/" + serviceName)
    }

    // TODO
    if (!serviceName.startsWith("SessionAuthentication")) {
        tasks.named(svc.id) { dependsOn deployModels, deployServices, deployClients, deployExtensions, deployDocumentation }
    }

    // Deployment
    def deployWebhookModels = tasks.register("generate${svc.name}WebhookModels", Sync) {
        println "Moving ${svc.name}-WebhookModels..."
        group "deploy"
        description "Move ${svc.name} webhook models into the repo."
        dependsOn "generate${svc.name}"
        outputs.upToDateWhen { false }

        from layout.buildDirectory.dir("services/$svc.id/src/Adyen/${serviceName}.Models")
        into layout.projectDirectory.dir("repo/Adyen/" + serviceName + "/Models")
    }

    def deployWebhookServices = tasks.register("generate${svc.name}WebhookServices", Sync) {
        println "Moving ${svc.name}-WebhookServices..."
        group "deploy"
        description "Move ${svc.name} webhook services into the repo."
        dependsOn "generate${svc.name}WebhookModels"
        outputs.upToDateWhen { false }

        from(layout.buildDirectory.dir("services/${svc.id}/src/Adyen/${serviceName}.Services").get().asFile) {
            exclude 'IApi.cs'
        }
        into layout.projectDirectory.dir("repo/Adyen/" + serviceName + "/Services")
    }

    def deployWebhookClients = tasks.register("generate${svc.name}WebhookClients", Sync) {
        println "Moving ${svc.name}-WebhookClients..."
        group "deploy"
        description "Move ${svc.name} webhook clients into the repo."
        dependsOn "generate${svc.name}WebhookServices"
        outputs.upToDateWhen { false }

        from (layout.buildDirectory.dir("services/$svc.id/src/Adyen/Client").get().asFile) {
            include 'HostConfiguration.cs', 'ClientUtils.cs', 'JsonSerializerOptionsProvider.cs'
        }
        into layout.projectDirectory.dir("repo/Adyen/" + serviceName + "/Client")
    }

    def deployWebhookExtensions = tasks.register("generate${svc.name}WebhookExtensions", Sync) {
        println "Moving ${svc.name}-WebhookExtensions..."
        group "generate"
        description "Move ${svc.name} webhook extensions into the repo."
        dependsOn "generate${svc.name}WebhookClients"
        outputs.upToDateWhen { false }

        from (layout.buildDirectory.dir("services/$svc.id/src/Adyen/Extensions").get().asFile) {
            include 'IHostBuilderExtensions.cs', 'IServiceCollectionExtensions.cs'
            rename('IHostBuilderExtensions.cs', 'HostBuilderExtensions.cs')
            rename('IServiceCollectionExtensions.cs', 'ServiceCollectionExtensions.cs')
        }
        into layout.projectDirectory.dir("repo/Adyen/" + serviceName + "/Extensions")
    }

    // Generate webhook handlers for webhooks
    if (serviceName.endsWith("Webhooks")) {
        // Apply extra config for webhooks only
        //templateDir.set("$projectDir/repo/templates-v7/csharp")
        // Generate webhook handlers for webhooks
        //configFile.set("$projectDir/config.yaml")
        tasks.named(svc.id) { dependsOn deployWebhookModels, deployWebhookServices, deployWebhookClients, deployWebhookExtensions }
    }
}

// Tests
//tasks.named('binlookup') {
//    doLast {
//        assert file("${layout.projectDirectory}/repo/src/Adyen/Model/BinLookup/Amount.cs").exists()
//        assert file("${layout.projectDirectory}/repo/src/Adyen/Service/BinLookupService.cs").exists()
//    }
//}
tasks.named('checkout') {
    doLast {
        assert file("${layout.projectDirectory}/repo/Adyen/Checkout/Models/Amount.cs").exists()
        assert file("${layout.projectDirectory}/repo/Adyen/Checkout/Services/PaymentsService.cs").exists()
    }
}
/* TODO: ACS does not exist.
tasks.named('acswebhooks') {
    doLast {
        assert file("${layout.projectDirectory}/repo/Adyen/Model/AcsWebhooks/Amount.cs").exists()
        assert !file("${layout.projectDirectory}/repo/Adyen/Service/AcsWebhooks").exists()
        assert !file("${layout.projectDirectory}/repo/Adyen/Service/AcsWebhooksService.cs").exists()
    }
}
*/