import com.adyen.sdk.Service
import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'adyen.sdk-automation-conventions'
}

project.ext {
    generator = 'csharp'
    templates = 'templates-v7/csharp/libraries/generichost'
}

def services = project.ext.services as List<Service>

services.each { Service svc ->
    def serviceName = project.ext.serviceNaming[svc.id] as String

    // Function to replace reserved words for a given directory.
    def replaceReservedWords = { File directory ->
        fileTree(directory).matching { include '**/*.cs' }.forEach { File file ->
            def text = file.text

            // See list of reserved words: https://github.com/OpenAPITools/openapi-generator/blob/master/docs/generators/csharp.md#reserved-words
            def replaceFromTo = [
                'VarEnvironment'  : 'Environment',
                'varEnvironment'  : 'environment',
                'VarVersion'      : 'Version',
                'varVersion'      : 'version',
                'VarTimeZone'     : 'TimeZone',
                'varTimeZone'     : 'timeZone',
                'VarConfiguration': 'Configuration',
                'varConfiguration': 'configuration',
                'wWWAuthenticate' : 'wwwAuthenticate',
            ]

            // Replaces reserved words with the name without the `Var` or `var`-prefix.
            replaceFromTo.each { from, to ->
                if (text.contains(from)) {
                    //println "Replacing ${from} â†’ ${to} in ${file.name}"
                    text = text.replace(from, to)
                }
            }

            file.text = text
        }
    }

    // Generation
    tasks.named("generate${svc.name}", GenerateTask) {
        println "Generating ${svc.name}..."

        // Sets suffix of api.
        apiNameSuffix.set("Service")

        // Sets apiPackage name for Services.
        apiPackage.set("${svc.name}.Services")

        // Sets modelPackageName.
        modelPackage.set("${svc.name}.Models")

        // Use typeMappings, Set<> is not a valid object in .NET.
        typeMappings.set(['set': 'HashSet'])

        // Use additionalProperties
        additionalProperties.putAll([
                // Target framework version.
                'targetFramework'               : 'net8.0',

                // Use library: generichost.
                'library'                       : 'generichost',

                // Your C# package name (convention: CamelCase).
                'packageName'                   : 'Adyen',

                // The name of your core-folder.
                'corePackageName'               : 'Core',

                // Service -suffix
                'apiName'                       : svc.name,

                // Make API response's headers case-insensitive
                'caseInsensitiveResponseHeaders': 'false',

                // Override Equals and GetHashCode methods
                'equatable'                     : 'false',

                // Name of the API service.
                'serviceName'                   : serviceName,

                // Use nullable annotations in the project, only supported on C# 8 & ASP.NET Core 3.1+
                // Starting in .NET 6.0 the default is true.
                'nullableReferenceTypes'        : 'true',

                // Use DateTime to model date properties even if DateOnly supported. (.NET 6.0+ only)
                'useDateTimeOffset'             : 'true',

                // Use the discriminator's mapping in oneOf to speed up the model lookup.
                // IMPORTANT NOTE: Validation (e.g. one and only one match in oneOf's schemas) will be skipped.
                'useOneOfDiscriminatorLookup'   : 'true',

                // Source generation is a feature that lets the compiler generate optimized code at build time, making JSON (de)serialization faster.
                // This flag uses the SourceGenerationContext.mustache template to:
                // 1. `WriteIndented = true` -> Prettify, by indenting the JSON
                // 2. `GenerationMode = JsonSourceGenerationMode.Metadata | JsonSourceGenerationMode.Serialization`
                //     This sets the generation mode using a bitwise OR between two flags:
                //     - Metadata: Generates metadata needed to support reflection-free serialization.
                //     - Serialization: Generates methods for serializing objects to JSON.
                //'useSourceGeneration': 'false',
        ])

        if (serviceName.endsWith("Webhooks")) {
            println("Set Config to generate WebhookHandler for ${serviceName}")
            configFile.set("$projectDir/config.yaml")
        }
    }

    def deployModels = tasks.register("generate${svc.name}Models", Sync) {
        println "Moving ${svc.name}-Models..."
        group "deploy"
        description "Move ${svc.name} models into the repo."
        dependsOn "generate${svc.name}"
        outputs.upToDateWhen { false }

        // Delete existing /Models directory.
        doFirst {
            delete layout.projectDirectory.dir("repo/Adyen/${serviceName}/Models")
        }

        from(layout.buildDirectory.dir("services/${svc.id}/src/Adyen/${serviceName}.Models").get().asFile) {
            eachFile { FileCopyDetails copyDetails ->
                replaceReservedWords(copyDetails.file)
            }
        }
        into layout.projectDirectory.dir("repo/Adyen/${serviceName}/Models")
    }

    def deployServices = tasks.register("generate${svc.name}Services", Sync) {
        println "Moving ${svc.name}-Services..."
        group "deploy"
        description "Move ${svc.name} into the repo."
        dependsOn "generate${svc.name}Models"
        outputs.upToDateWhen { false }
        onlyIf { !svc.webhook } // Skip webhooks - This task is for generating service classes only, see deployWebhookHandlers instead.

        // Delete existing /Services directory.
        doFirst {
            delete layout.projectDirectory.dir("repo/Adyen/${serviceName}/Services")
        }

        from(layout.buildDirectory.dir("services/${svc.id}/src/Adyen/${serviceName}.Services").get().asFile) {
            exclude 'IApi.cs'
            eachFile { FileCopyDetails copyDetails ->
                replaceReservedWords(copyDetails.file)
            }
        }
        into layout.projectDirectory.dir("repo/Adyen/${serviceName}/Services")
    }

    def deployClients = tasks.register("generate${svc.name}Clients", Sync) {
        println "Moving ${svc.name}-Clients..."
        group "deploy"
        description "Move ${svc.name} into the repo."
        dependsOn "generate${svc.name}Services"
        outputs.upToDateWhen { false }

        // Delete existing /Clients directory.
        doFirst {
            delete layout.projectDirectory.dir("repo/Adyen/${serviceName}/Clients")
        }

        from(layout.buildDirectory.dir("services/$svc.id/src/Adyen/Client").get().asFile) {
            include 'HostConfiguration.cs', 'ClientUtils.cs', 'JsonSerializerOptionsProvider.cs', 'ApiKeyToken.cs'
            eachFile { FileCopyDetails copyDetails ->
                replaceReservedWords(copyDetails.file)

                // Do not include the `Adyen.{{Webhook}}.Services` as namespace for webhooks.
                if (copyDetails.name == 'HostConfiguration.cs' && serviceName.endsWith("Webhooks")) {
                    copyDetails.file.text = copyDetails.file.text.replace("using Adyen.${serviceName}.Services;", "")
                    println("Removed Adyen.${{serviceName}}.Services namespace in HostConfiguration.cs")
                }
            }
        }
        into layout.projectDirectory.dir("repo/Adyen/${serviceName}/Client")
    }

    def deployExtensions = tasks.register("generate${svc.name}Extensions", Sync) {
        println "Moving ${svc.name}-Extensions..."
        group "generate"
        description "Move ${svc.name} into the repo."
        dependsOn "generate${svc.name}Clients"
        outputs.upToDateWhen { false }

        // Delete existing /Extensions directory.
        doFirst {
            delete layout.projectDirectory.dir("repo/Adyen/${serviceName}/Extensions")
        }

        from(layout.buildDirectory.dir("services/$svc.id/src/Adyen/Extensions").get().asFile) {
            include 'IHostBuilderExtensions.cs', 'IServiceCollectionExtensions.cs'
            rename('IHostBuilderExtensions.cs', 'HostBuilderExtensions.cs')
            rename('IServiceCollectionExtensions.cs', 'ServiceCollectionExtensions.cs')
        }
        into layout.projectDirectory.dir("repo/Adyen/${serviceName}/Extensions")
    }

    def deployDocumentation = tasks.register("generate${svc.name}Documentation", Sync) {
        println "Moving ${svc.name}-Documentation..."
        group "generate"
        description "Move ${svc.name} into the repo."
        dependsOn "generate${svc.name}Services"
        outputs.upToDateWhen { false }

        doFirst {
            // Delete existing /docs directory.
            delete layout.projectDirectory.dir("repo/docs/${serviceName}")

            def docsDirectory = layout.projectDirectory.dir("repo/docs").getAsFile()

            // Create /docs directory if it doesn't exist.
            if (!docsDirectory.exists()) {
                docsDirectory.mkdirs()
                println "Created /docs directory: ${docsDirectory}"
            }
        }

        from(layout.buildDirectory.dir("services/$svc.id/docs/apis").get().asFile)
        into layout.projectDirectory.dir("repo/docs/${serviceName}")
    }

    def deployWebhookHandlers = tasks.register("generate${svc.name}WebhookHandlers") {
        group 'deploy'
        description "Move $svc.name handlers into the repo."
        dependsOn "generate${svc.name}Models"
        outputs.upToDateWhen { false }

        doLast {
            if (serviceName.endsWith("Webhooks")) {
                // Find generated WebhookHandler.cs.
                def webhookHandlerFile = layout.projectDirectory.file("build/services/$svc.id/src/Adyen/SupportingFiles/WebhookHandler.cs").getAsFile()
                // Move file to the /handlers directory and prefix with the serviceName (e.g. WebhookHandler.cs -> `Configuration`WebhooksHandler.cs).
                def handlerDirectory = layout.projectDirectory.file("repo/Adyen/${serviceName}/Handlers").asFile

                // Create /handlers directory if it doesn't exist.
                if (!handlerDirectory.exists()) {
                    handlerDirectory.mkdirs()
                }

                def targetFile = new File(handlerDirectory, svc.name + "Handler.cs")

                if (webhookHandlerFile.exists()) {
                    webhookHandlerFile.renameTo(targetFile)
                    println "Moved ${webhookHandlerFile} to ${targetFile}"
                } else {
                    println "Source file does not exist: ${webhookHandlerFile}"
                }
            }
        }
    }

    tasks.named(svc.id) { dependsOn deployModels, deployServices, deployClients, deployExtensions, deployDocumentation, deployWebhookHandlers }
}

// Tests
//tasks.named('binlookup') {
//    doLast {
//        assert file("${layout.projectDirectory}/repo/src/Adyen/Model/BinLookup/Amount.cs").exists()
//        assert file("${layout.projectDirectory}/repo/src/Adyen/Service/BinLookupService.cs").exists()
//    }
//}
tasks.named('checkout') {
    doLast {
        assert file("${layout.projectDirectory}/repo/Adyen/Checkout/Models/Amount.cs").exists()
        assert file("${layout.projectDirectory}/repo/Adyen/Checkout/Services/PaymentsService.cs").exists()
    }
}
/* TODO: ACS does not exist.
tasks.named('acswebhooks') {
    doLast {
        assert file("${layout.projectDirectory}/repo/Adyen/Model/AcsWebhooks/Amount.cs").exists()
        assert !file("${layout.projectDirectory}/repo/Adyen/Service/AcsWebhooks").exists()
        assert !file("${layout.projectDirectory}/repo/Adyen/Service/AcsWebhooksService.cs").exists()
    }
}
*/