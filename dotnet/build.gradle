import com.adyen.sdk.Service
import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'adyen.sdk-automation-conventions'
}

project.ext {
    generator = 'csharp'
    templates = 'templates-v7/csharp/libraries/generichost'
}

def services = project.ext.services as List<Service>

services.each { Service svc ->
    def serviceName = project.ext.serviceNaming[svc.id] as String

    // Generation
    tasks.named("generate${svc.name}", GenerateTask) {
        println "Generating ${svc.name}..."
        apiPackage.set("${svc.name}.Services")
        apiNameSuffix.set("Service")
        modelPackage.set("${svc.name}.Models")
/*        reservedWordsMappings.putAll([
                "version": "Version",
//                "Version": "Version",
//                "VarVersion": "Version",
//                "varVersion": "Version",
//                "timeZone": "TimeZone",
//                "environment": "Environment"
        ])*/
//        parameterNameMappoing.putAll([
//                "gag": "Dsgs"
//        ])

        additionalProperties.putAll([
                // Target framework version
                'targetFramework': 'net8.0',

                'library': 'generichost',

                // Your C# package name (convention: CamelCase).
                'packageName': 'Adyen',

                // The name of your core-folder.
                'corePackageName': 'Core',

                // Service -suffix
                'apiName': svc.name,

                // Make API response's headers case-insensitive
                'caseInsensitiveResponseHeaders': 'false',

                // Override Equals and GetHashCode methods
                'equatable': 'true',

                // Name of the API service.
                'serviceName': serviceName,

                // Use nullable annotations in the project, only supported on C# 8 & ASP.NET Core 3.1+
                // Starting in .NET 6.0 the default is true.
                'nullableReferenceTypes': 'true',

                // Use DateTime to model date properties even if DateOnly supported. (.NET 6.0+ only)
                'useDateTimeOffset': 'true',

                // Use the discriminator's mapping in oneOf to speed up the model lookup.
                // IMPORTANT NOTE: Validation (e.g. one and only one match in oneOf's schemas) will be skipped.
                'useOneOfDiscriminatorLookup': 'true',

//                'reserved-words-mappings':  [
//                    'Version': 'VersionVV',
//                    'version': 'Versionvv',
//                    'VarVersion': 'VersionVVar',
//                    'varVersion': 'Versionvvar',
//                    'TimeZone': 'TimeZone',
//                    'Environment': 'Environment',
//                ]
                //'SET_PRIMITIVE_TYPES_TO_NULLABLE': 'true',
        ])
    }

    // Deployment
    def deployModels = tasks.register("deploy${svc.name}Models", Sync) {
        println "Moving ${svc.name}-Models..."
        group "deploy"
        description "Move ${svc.name} models into the repo."
        dependsOn "generate${svc.name}"
        outputs.upToDateWhen { false }

        from layout.buildDirectory.dir("services/$svc.id/src/Adyen/${serviceName}.Models")
        into layout.projectDirectory.dir("repo/Adyen/" + serviceName + "/Models")
    }

    def deployServices = tasks.register("deploy${svc.name}Services", Sync) {
        println "Moving ${svc.name}-Services..."
        group "deploy"
        description "Move ${svc.name} into the repo."
        dependsOn "deploy${svc.name}Models"
        outputs.upToDateWhen { false }

        from(layout.buildDirectory.dir("services/${svc.id}/src/Adyen/${serviceName}.Services").get().asFile) {
            exclude 'IApi.cs'
        }
        into layout.projectDirectory.dir("repo/Adyen/" + serviceName + "/Services")
    }

    def deployClients = tasks.register("deploy${svc.name}Clients", Sync) {
        println "Moving ${svc.name}-Clients..."
        group "deploy"
        description "Move ${svc.name} into the repo."
        dependsOn "deploy${svc.name}Services"
        outputs.upToDateWhen { false }

        from (layout.buildDirectory.dir("services/$svc.id/src/Adyen/Client").get().asFile) {
            include 'HostConfiguration.cs', 'ClientUtils.cs'
        }
        into layout.projectDirectory.dir("repo/Adyen/" + serviceName + "/Client")
    }

    def deployExtensions = tasks.register("deploy${svc.name}Extensions", Sync) {
        println "Moving ${svc.name}-Extensions..."
        group "deploy"
        description "Move ${svc.name} into the repo."
        dependsOn "deploy${svc.name}Clients"
        outputs.upToDateWhen { false }

        from (layout.buildDirectory.dir("services/$svc.id/src/Adyen/Extensions").get().asFile) {
            include 'IHostBuilderExtensions.cs', 'IServiceCollectionExtensions.cs'
            rename('IHostBuilderExtensions.cs', 'HostBuilderExtensions.cs')
            rename('IServiceCollectionExtensions.cs', 'ServiceCollectionExtensions.cs')
        }
        into layout.projectDirectory.dir("repo/Adyen/" + serviceName + "/Extensions")
    }

    tasks.named(svc.id) { dependsOn deployModels, deployServices, deployClients, deployExtensions }
}

// Tests
//tasks.named('binlookup') {
//    doLast {
//        assert file("${layout.projectDirectory}/repo/src/Adyen/Model/BinLookup/Amount.cs").exists()
//        assert file("${layout.projectDirectory}/repo/src/Adyen/Service/BinLookupService.cs").exists()
//    }
//}
tasks.named('checkout') {
    doLast {
        assert file("${layout.projectDirectory}/repo/Adyen/Checkout/Models/Amount.cs").exists()
        assert file("${layout.projectDirectory}/repo/Adyen/Checkout/Services/PaymentsService.cs").exists()
    }
}
/* TODO: ACS does not exist.
tasks.named('acswebhooks') {
    doLast {
        assert file("${layout.projectDirectory}/repo/Adyen/Model/AcsWebhooks/Amount.cs").exists()
        assert !file("${layout.projectDirectory}/repo/Adyen/Service/AcsWebhooks").exists()
        assert !file("${layout.projectDirectory}/repo/Adyen/Service/AcsWebhooksService.cs").exists()
    }
}
*/