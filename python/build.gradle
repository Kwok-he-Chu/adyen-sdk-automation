import com.adyen.sdk.Service
import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'adyen.sdk-automation-conventions'
}

project.ext {
    generator = 'python'
}

List<Service> services = project.ext.services
services.find { it.id == 'payment' }.small = false

// Service renaming
Map<String, String> serviceNaming = project.ext.serviceNamingCamel
serviceNaming.putAll([
        'binlookup'            : 'binlookup',
        'payment'              : 'payments',
        'posterminalmanagement': 'terminal',
        'payout'               : 'payouts'
])

services.<Service> each { Service svc ->
    String serviceName = serviceNaming[svc.id]

    // Generation
    tasks.named("generate${svc.name}", GenerateTask) {
        engine.set('mustache')
        configFile.set("$projectDir/config.yaml")
        additionalProperties.putAll([
                'serviceName': serviceName,
        ])
    }

    // Deployment
    def deployServices = tasks.register("deploy${svc.name}Services", Sync) {
        group 'deploy'
        description "Deploy $svc.name into the repo."
        dependsOn "generate$svc.name"
        outputs.upToDateWhen { false }
        onlyIf { !svc.webhook }

        doFirst {
            delete layout.projectDirectory.dir("repo/Adyen/services/" + serviceName)
        }


        from layout.buildDirectory.dir("services/$svc.id/openapi_client/api")
        include '*_api.py'
        into layout.projectDirectory.dir("repo/Adyen/services/" + serviceName)

        from(layout.buildDirectory.dir("services/$svc.id/api")) {
            include 'api-single.py'
            rename 'api-single.py', '__init__.py'
        }
    }

    tasks.named(svc.id) { dependsOn deployServices }
}

// TODO: Review how to generalize this to other libs
/**
 * Configures a service lifecycle tasks to perform a final sanity check: ensure that given files exists at the expected location.
 * @param serviceName the service to be checked
 * @param files the expected files
 */
def assertServiceFilesExist(String serviceName, List<String> files) {
    tasks.named(serviceName) {
        // Hooks into this lifecycle task to do a sanity check: ensure that the given files
        // exists in the expected location
        doLast {
            files.forEach {
                assert file("${layout.projectDirectory}/repo/Adyen/services/${serviceName}/" + it).exists()
            }
        }
    }
}

assertServiceFilesExist('checkout', ["payments_api.py", "__init__.py"])
assertServiceFilesExist('capital', ["__init__.py", "grants_api.py", "grant_offers_api.py", "grant_accounts_api.py"])
