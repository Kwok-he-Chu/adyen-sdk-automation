import com.adyen.sdk.Service
import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'adyen.sdk-automation-conventions'
}

project.ext {
    generator = 'java'
}

def services = project.ext.services as List<Service>

tasks.withType(GenerateTask).configureEach {
    def serviceId = project.ext.serviceName.toLowerCase()
    def modelNamespace = "com.adyen.model.${serviceId}"
    
    templateDir.set("$projectDir/repo/templates-v7")
    library.set("jersey3")
    modelPackage.set(modelNamespace.replace('/', '.'))
    apiPackage.set("com.adyen.service.${serviceId}")
    apiNameSuffix.set('Api')
    additionalProperties.putAll([
            'dateLibrary'    : 'java8',
            'openApiNullable': 'false',
            'enumPropertyNaming': "legacy",
            'javaxPackage': 'jakarta',
            "containerDefaultToNull" : "true"
    ])

    if (serviceId.endsWith("webhooks")) {
        // for webhooks only apply extra config.yaml (to generate WebhookHandler)
        configFile.set("$projectDir/config.yaml")
    } else {
        // for APIs only add custom code to handle nullable properties
        additionalProperties.handleNullableProperties = true
    }

}

// Deployment: copy and rename models/services
services.each { Service svc ->

    def serviceName = project.ext.serviceNaming[svc.id] as String
    def serviceId = serviceName.toLowerCase()

    // words replacement to enforce naming conventions (after code generation)
    def replaceReservedWords = { File directory ->

        fileTree(directory).matching { include '**/*.java' }.forEach { File file ->
            def text = file.text

            def replaceFromTo = [
                    'wwWAuthenticate' : 'wwwAuthenticate',
            ]

            replaceFromTo.each { from, to ->
                //println "Replacing ${from} â†’ ${to} in ${file.name}"
                text = text.replace(from, to)
            }
            file.text = text
        }
    }


    // Copy models
    def deployModels = tasks.register("deploy${svc.name}Models", Copy) {
        group 'deploy'
        description "Deploy $svc.name models into the repo."
        dependsOn "generate$svc.name"
        outputs.upToDateWhen { false }

        // delete existing models
        doFirst {
            delete layout.projectDirectory.dir("repo/src/main/java/com/adyen/model/${serviceId}")
        }

        // copy newly generated models
        from layout.buildDirectory.dir("services/$svc.id/src/main/java/com/adyen/model")
        into layout.projectDirectory.dir("repo/src/main/java/com/adyen/model")

        // post processing
        doLast {
            replaceReservedWords(layout.projectDirectory.dir("repo/src/main/java/com/adyen/model/${serviceId}").asFile)
        }
    }

    // Copy services
    def deployServices = tasks.register("deploy${svc.name}Services", Copy) {
        group 'deploy'
        description "Deploy $svc.name services into the repo."
        dependsOn "deploy${svc.name}Models"
        outputs.upToDateWhen { false }

        // delete existing services
        doFirst {
            delete layout.projectDirectory.dir("repo/src/main/java/com/adyen/service/${serviceId}")
        }

        // copy newly generated services
        from layout.buildDirectory.dir("services/$svc.id/src/main/java/com/adyen/service/${serviceId}")
        into layout.projectDirectory.dir("repo/src/main/java/com/adyen/service/" + serviceId)

        // post processing
        doLast {
            replaceReservedWords(layout.projectDirectory.dir("repo/src/main/java/com/adyen/service/${serviceId}").asFile)
        }
    }

    // Copy serializers
    def deploySerializers = tasks.register("deploy${svc.name}Serializers", Copy) {
        group 'deploy'
        description "Deploy $svc.name serializers into the repo."
        dependsOn "deploy${svc.name}Services"
        outputs.upToDateWhen { false }

        // copy serializer JSON.java from service folder (if found) into model folder
        def jsonJavaFileModelFolder = layout.buildDirectory.file("services/$svc.id/src/main/java/com/adyen/service/JSON.java")

        from jsonJavaFileModelFolder
        into layout.projectDirectory.file("repo/src/main/java/com/adyen/model/${serviceId}")

        // copy serializer JSON.java from adyen folder (if found) into model folder
        def jsonJavaFileAdyenFolder = layout.buildDirectory.file("services/$svc.id/src/main/java/com/adyen/JSON.java")

        from jsonJavaFileAdyenFolder
        into layout.projectDirectory.file("repo/src/main/java/com/adyen/model/${serviceId}")

    }

    // Copy Webhook handlers
    def deployWebhookHandlers = tasks.register("deploy${svc.name}Handlers") {
        group 'deploy'
        description "Move $svc.name handlers into the repo."
        dependsOn "deploy${svc.name}Services"
        outputs.upToDateWhen { false }

        doLast {
            // find generated WebhookHandler.java
            def sourceFile = layout.projectDirectory.file("repo/src/main/java/com/adyen/model/WebhookHandler.java").asFile
            // move to service folder and rename (i.e. WebhookHandler.java to ConfigurationWebhooksHandler.java)
            def targetDir = layout.projectDirectory.file("repo/src/main/java/com/adyen/model/${serviceId}").asFile
            def targetFile = new File(targetDir, svc.name + "Handler.java")

            if (sourceFile.exists()) {
                sourceFile.renameTo(targetFile)
                println "Moved ${sourceFile} to ${targetFile}"
            } else {
                println "Source file does not exist: ${sourceFile}"
            }
        }
    }

    tasks.named(svc.id) { dependsOn deployModels, deployServices, deploySerializers, deployWebhookHandlers }
}

// Test binlookup  generation
tasks.named('binlookup') {
    doLast {
        assert file("${layout.projectDirectory}/repo/src/main/java/com/adyen/model/binlookup/Amount.java").exists()
        assert file("${layout.projectDirectory}/repo/src/main/java/com/adyen/service/BinLookupApi.java").exists()
        // verify DefaultApi class no longer exists (it has been renamed to BinLookupApi)
        assert !file("${layout.projectDirectory}/repo/src/main/java/com/adyen/service/binlookup/DefaultApi.java").exists()
    }
}
// Test checkout generation
tasks.named('checkout') {
    doLast {
        assert file("${layout.projectDirectory}/repo/src/main/java/com/adyen/model/checkout/Amount.java").exists()
        assert file("${layout.projectDirectory}/repo/src/main/java/com/adyen/service/checkout/PaymentsApi.java").exists()
    }
}
// Test acswebhooks generation
tasks.named('acswebhooks') {
    doLast {
        assert file("${layout.projectDirectory}/repo/src/main/java/com/adyen/model/acswebhooks/Amount.java").exists()
        // verify no service package is created for a webhook
        assert !file("${layout.projectDirectory}/repo/src/main/java/com/adyen/service/acswebhooks").exists()
        // verify no API class is created for a webhook
        assert !file("${layout.projectDirectory}/repo/src/main/java/com/adyen/service/AcsWebhooksApi.java").exists()
        // verify Webhook Handler is created
        assert file("${layout.projectDirectory}/repo/src/main/java/com/adyen/model/acswebhooks/AcsWebhooksHandler.java").exists()
    }
}

// Test balanceplatform generation
tasks.named('balanceplatform') {
    doLast {
        // HTTP header not found in signature
        def fileContent = file("${layout.projectDirectory}/repo/src/main/java/com/adyen/service/balanceplatform/ScaAssociationManagementApi.java").text
        assert !fileContent.contains("wwwAuthenticate"), "'wwwAuthenticate' (HTTP header) should not be part of a method signature in ScaAssociationManagementApi.java"
    }
}

